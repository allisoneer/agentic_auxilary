# Use bash with strict flags
set shell := ["bash", "-euo", "pipefail", "-c"]

# CI/output mode detection
ci := env("CI", "false")
output_mode := env("OUTPUT_MODE", if ci == "true" { "normal" } else { "minimal" })

# Wrapper path
WRAP := "../tools/cargo-wrap.sh"

# Packages in the workspace
PKGS := "-p linear-schema -p linear-queries -p linear-tools"

default: help

help:
  @echo "linear_tools commands:"
  @echo "  just check      # fmt-check on all crates + clippy on all"
  @echo "  just test       # tests across linear-schema, linear-queries, linear-tools"
  @echo "  just build      # build all three crates"
  @echo "  just fmt        # format all crates"
  @echo "  just fmt-check  # check formatting for all crates"
  @echo "  just test-live  # run live Linear API tests (requires LINEAR_API_KEY and LINEAR_TEST_TEAM_ID)"

fmt:
  cargo fmt --manifest-path linear-schema/Cargo.toml
  cargo fmt --manifest-path linear-queries/Cargo.toml
  cargo fmt --manifest-path linear-tools/Cargo.toml

fmt-check:
  {{WRAP}} fmt-check --manifest-path linear-schema/Cargo.toml
  {{WRAP}} fmt-check --manifest-path linear-queries/Cargo.toml
  {{WRAP}} fmt-check --manifest-path linear-tools/Cargo.toml

check:
  just fmt-check
  {{WRAP}} clippy {{PKGS}} --all-targets -- -D warnings

test:
  {{WRAP}} test {{PKGS}}

build:
  {{WRAP}} build {{PKGS}}

# Run opt-in live tests (ignored by default)
test-live:
  LINEAR_LIVE_TESTS=1 {{WRAP}} test -p linear-tools --test live -- --ignored
